apply plugin: "com.android.application"
apply plugin: "com.google.android.gms.oss-licenses-plugin"
apply plugin: "kotlin-android"

android {
    compileSdkVersion 34
    buildToolsVersion "34.0.0"
    namespace "org.radarcns.detail"

    defaultConfig {
        applicationId "org.radarcns.detail"
        minSdkVersion 26
        targetSdkVersion 33
        versionCode 62
        versionName "1.2.1"
        manifestPlaceholders = ["appAuthRedirectScheme": "org.radarbase.passive.app"]
        multiDexEnabled true
        ndkVersion "25.2.9519653"
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            ndk {
                debugSymbolLevel = "full"
            }
        }
        debug {
            shrinkResources false
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
    packagingOptions {
        resources {
            excludes += ["META-INF/ASL2.0", "META-INF/LICENSE"]
        }
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_17
        sourceCompatibility JavaVersion.VERSION_17
    }
    buildFeatures {
        viewBinding true
    }
    flavorDimensions "channel"
    productFlavors {
        playStore {
            dimension "channel"
            versionNameSuffix "-playStore"
        }
        selfRelease {
            dimension "channel"
            versionNameSuffix "-selfRelease"
        }
    }
    lint {
        abortOnError false
    }
}

configurations.all {
    resolutionStrategy.cacheDynamicVersionsFor 0, "seconds"
    resolutionStrategy.cacheChangingModulesFor 0, "seconds"
}

repositories {
    flatDir { dirs "libs" }
    google()
    mavenLocal()
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://repo.thehyve.nl/content/repositories/releases' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    api "org.radarbase:radar-commons-android:$radar_commons_android_version"

    implementation "org.radarbase:radar-android-login-qr:$radar_commons_android_version"

    implementation platform("com.google.firebase:firebase-bom:$firebase_bom_version")
    implementation "com.google.android.gms:play-services-oss-licenses:$play_services_oss_licenses_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation "org.slf4j:slf4j-api:$slf4j_api_version"

    implementation "com.google.firebase:firebase-config"
    implementation "com.google.firebase:firebase-crashlytics"
    implementation "com.google.firebase:firebase-analytics"

    implementation "com.gitlab.mvysny.slf4j:slf4j-handroid:$slf4j_handroid_version"

    implementation "org.radarbase:radar-android-phone:$radar_commons_android_version"
    implementation "org.radarbase:radar-android-phone-usage:$radar_commons_android_version"
    selfReleaseImplementation "org.radarbase:radar-android-phone-telephony:$radar_commons_android_version"
    implementation "org.radarbase:radar-android-empatica:$radar_commons_android_version"
    implementation "org.radarbase:radar-android-application-status:$radar_commons_android_version"
    implementation "org.radarbase:radar-android-weather:$radar_commons_android_version"
    implementation "org.radarbase:radar-android-audio:$radar_commons_android_version"
    implementation "org.radarbase:radar-android-faros:$radar_commons_android_version"
    implementation "org.radarbase:radar-android-google-sleep:$radar_commons_android_version"
    implementation "org.radarbase:radar-android-google-activity:$radar_commons_android_version"
    implementation "org.radarbase:radar-android-google-places:$radar_commons_android_version"

    implementation "org.radarbase:radar-android-polar:$radar_commons_android_version"

    implementation "androidx.lifecycle:lifecycle-process:$lifecycle_process_version"
    implementation "androidx.legacy:legacy-support-v4:$legacy_support_version"
    implementation "com.google.android.material:material:$material_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation "androidx.fragment:fragment-ktx:$fragment_version"

    testImplementation "junit:junit:$junit_version"
    testRuntimeOnly "org.slf4j:slf4j-simple:$slf4j_simple_version"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "17"
        apiVersion = "1.7"
        languageVersion = "1.7"
    }
}

// Needed for Firebase. Put at the bottom so it can detect the Firebase version.
apply plugin: "com.google.gms.google-services"
apply plugin: "com.google.firebase.crashlytics"
